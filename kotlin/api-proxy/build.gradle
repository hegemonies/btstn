buildscript {
    ext {
        ktorVersion="1.4.1"
        kotlinVersion="1.4.10"
        logbackVersion="1.2.1"
        exposedVersion = "0.24.1"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
    id "com.google.cloud.tools.jib" version "2.6.0"
    id "com.github.johnrengelman.shadow" version "6.1.0"
}

apply plugin: 'kotlin'
apply plugin: 'application'

mainClassName = "io.ktor.server.netty.EngineMain"

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src']
    test.kotlin.srcDirs = test.java.srcDirs = ['test']
    main.resources.srcDirs = ['resources']
    test.resources.srcDirs = ['testresources']
}

dependencies {
    implementation(
            // Kotlin standard library
            "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion",
            "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion",

            // Logger library
            "ch.qos.logback:logback-classic:$logbackVersion",

            // HTTP server library - Ktor
            "io.ktor:ktor-server-netty:$ktorVersion",
            "io.ktor:ktor-metrics:$ktorVersion",
            "io.ktor:ktor-server-core:$ktorVersion",
            "io.ktor:ktor-server-host-common:$ktorVersion",

            // JSON library
            "io.ktor:ktor-jackson:$ktorVersion",

            // Database library
            "org.jetbrains.exposed:exposed-core:$exposedVersion",
            "org.jetbrains.exposed:exposed-dao:$exposedVersion",
            "org.jetbrains.exposed:exposed-jdbc:$exposedVersion",

            // Postgresql driver
            "org.postgresql:postgresql:42.2.16",

            // DTO
            project(":kotlin-dto"),
    )
    testImplementation "io.ktor:ktor-server-tests:$ktorVersion"
}

jib {
    to {
        image = "btstn/api-proxy:dev"
    }
    container {
        user = "1000"
        creationTime = "USE_CURRENT_TIMESTAMP"
    }
    allowInsecureRegistries = true
}

shadowJar {
    archiveFileName = "api-proxy.jar"
}
